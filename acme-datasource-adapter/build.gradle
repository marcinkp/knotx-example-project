plugins {
  id 'java'
}

description = 'Acme Project - Sample datasource adapter'

defaultTasks 'build'


dependencies {
  compileOnly "io.knotx:knotx-dependencies:$knotxVersion"

  compileOnly 'io.knotx:knotx-core'
  compileOnly 'io.knotx:knotx-databridge-api'
  compileOnly 'io.vertx:vertx-core'
  compileOnly 'io.vertx:vertx-rx-java2'
  compileOnly 'io.vertx:vertx-codegen'
  compileOnly 'io.vertx:vertx-service-proxy'
  compileOnly 'io.vertx:vertx-jdbc-client'
  compileOnly "org.hsqldb:hsqldb:2.4.0"

  testCompile 'org.junit.jupiter:junit-jupiter-api'
  testCompile 'org.junit.jupiter:junit-jupiter-engine'
  testCompile 'org.junit.jupiter:junit-jupiter-params'
  testCompile 'org.junit.jupiter:junit-jupiter-migrationsupport'
  testCompile 'org.junit.vintage:junit-vintage-engine'
  testCompile 'uk.co.datumedge:hamcrest-json'
  testCompile 'org.hamcrest:hamcrest-all'
  testCompile 'com.github.tomakehurst:wiremock'

  testCompileOnly 'io.vertx:vertx-codegen'
  testCompile 'io.vertx:vertx-config'
  testCompile 'io.vertx:vertx-junit5'
  testCompile 'io.knotx:knotx-mocks'
  testCompile 'io.knotx:knotx-junit5'
  testCompile "org.hsqldb:hsqldb:2.4.0"
}
sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
  test {
    resources {
      srcDirs += 'src/test/resources'
    }
  }
}

clean.doFirst {
  file('src/main/generated').deleteDir()
}

task annotationProcessing(type: JavaCompile, group: 'build') { // codegen
  source = sourceSets.main.java
  classpath = configurations.compile + configurations.compileOnly
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
      "-proc:only",
      "-processor", "io.vertx.codegen.CodeGenProcessor",
      "-Acodegen.output=${destinationDir.absolutePath}"
  ]
}

task annotationProcessingCleanup() {
  doLast {
    delete "${project.projectDir}/src/main/generated"
    println "Removing: ${project.projectDir}/src/main/generated"
  }
}

annotationProcessing.dependsOn annotationProcessingCleanup

compileJava.dependsOn annotationProcessing
